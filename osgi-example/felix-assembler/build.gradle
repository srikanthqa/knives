apply plugin: 'java'
apply plugin: 'maven'

defaultTasks 'startFelix'

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	felix {
		transitive = false
	}
	
	// any that declared as bundle on maven
	// i.e. everything included, so we don't need to include
	// transitive dependencies
	bundle {
		transitive = false
	}
	
	repack 
}

dependencies {
	felix group: 'org.apache.felix', name: 'org.apache.felix.main', version: felixVersion
	
	bundle group: 'org.osgi', name: 'org.osgi.compendium', version: osgiCompendiumVersion
	
	bundle group: 'commons-fileupload', name: 'commons-fileupload', version: commonsFileuploadVersion
	bundle group: 'commons-io', name: 'commons-io', version: commonsIoVersion
	
	bundle group: 'org.apache.felix', name: 'org.apache.felix.webconsole', version: felixWebconsoleVersion
	bundle group: 'org.apache.felix', name: 'org.apache.felix.http.servlet-api', version: felixServletVersion
	bundle group: 'org.apache.felix', name: 'org.apache.felix.http.bundle', version: felixHttpVersion
	bundle group: 'org.apache.felix', name: 'org.apache.felix.eventadmin', version: felixEventAdminVersion
 
	bundle group: 'org.slf4j', name: 'osgi-over-slf4j', version: slf4jVersion
	bundle group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
	bundle group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jVersion
	bundle group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
	bundle group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
	bundle group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
	
	bundle group: 'org.apache.aries.blueprint', name: 'org.apache.aries.blueprint', version: ariesBlueprintVersion
	bundle group: 'org.apache.aries.proxy', name: 'org.apache.aries.proxy', version: ariesProxyVersion
	bundle group: 'org.apache.aries', name: 'org.apache.aries.util', version: ariesUtilVersion
	
	repack group: 'org.json', name: 'json', version: '20141113'
}

def findJar(name) {
	return configurations.repack.filter { it.name.startsWith(name) }
}

task assembleBundles(type: Copy) {
	from configurations.bundle
	into "${projectDir}/build/felix/bundle"
}

task assembleConfig(type: Copy) {
	from "${projectDir}/src/main/config/config.properties"
	into "${projectDir}/build/felix/conf"
}

task assembleFelix(type: Copy, dependsOn: assembleConfig) {
	from configurations.felix
	into "${projectDir}/build/felix/bin"
}

task repackJson(type: Jar) {
	group = "Distribution"
	description = "Repack org.json:json with Osgi manifest"
	
	baseName = 'json'
	version = orgJsonVersion
	
	from zipTree(findJar('json').singleFile)
	
	manifest {
		attributes 'Bundle-Name': "org.json:json:${orgJsonVersion}"
		attributes 'Bundle-Version': '0'
		attributes 'Bundle-SymbolicName': 'org.json'
		attributes 'Bundle-ManifestVersion': '2'
		attributes 'Export-Package': 'org.json;version=0'
	}
	
	destinationDir = file("${projectDir}/build/felix/bundle")
}

task startFelix(type: JavaExec, dependsOn: [assembleFelix, assembleBundles]) {
	classpath fileTree("${projectDir}/build/felix/bin/")
	workingDir = file("${projectDir}/build/felix/")
	main = 'org.apache.felix.main.Main'
	systemProperties = [
			'logback.configurationFile' : "${projectDir}/src/main/resources/logback.xml"
		]
}

startFelix.dependsOn tasks.matching { it.name.startsWith('repack') }