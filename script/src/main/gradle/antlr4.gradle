apply plugin: 'java'

ext.versions = [
	antlr: "4.1"
]

ext.antlr = [
	grammarpackage: "com.custom.package",
	antlrSource: 'src/main/antlr',
	destinationDir: "src/generated-sources/java"
]

repositories { mavenCentral() }

configurations {
	antlr4 { description = "ANTLR4" }
	compile.extendsFrom antlr4
}

dependencies {
	antlr4 group: "org.antlr", name: "antlr4-runtime", version: versions.antlr
	antlr4 group: "org.antlr", name: "antlr4", version: versions.antlr
}

task antlrOutputDir { 
	mkdir(antlr.destinationDir) 
}

task generateGrammarSource(dependsOn: antlrOutputDir, type: JavaExec) {
	description = 'Generates Java sources from ANTLR4 grammars.'

	inputs.dir file(antlr.antlrSource)
	outputs.dir file(antlr.destinationDir)

	def grammars = fileTree(antlr.antlrSource).include('**/*.g4')

	main = 'org.antlr.v4.Tool'
	classpath = configurations.antlr4
	def pkg = antlr.grammarpackage.replaceAll("\\.", "/")
	args = [
		"-o",
		"${antlr.destinationDir}/${pkg}"/*, "-atn"*/,
		"-visitor",
		"-package",
		antlr.grammarpackage,
		grammars.files
	].flatten()
}

compileJava {
	dependsOn generateGrammarSource
	source antlr.destinationDir
}

clean { delete antlr.destinationDir }